plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

private Integer generateVersionCode() {
    return project.minimumSdkVersion * 10000000 + project.versionMajor * 10000 + project.versionMinor * 100 + project.versionPatch
}

private String generateVersionName() {
    return "${project.versionMajor}.${project.versionMinor}.${project.versionPatch}"
}

project.ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 0
    minimumSdkVersion = 21
    appName = "com.huma.sample"
}

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId project.appName
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 32
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "API_URL", "\"https://newsapi.org/v2/\"")
    }

    buildFeatures {
        dataBinding = true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.huma.sample'

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "huma-sample-${variant.versionName}-${variant.versionCode}.apk"
        }
    }
}

dependencies {

    // Leanback
    implementation "androidx.leanback:leanback:1.0.0"
    implementation "androidx.leanback:leanback-paging:1.1.0-alpha09"

    // UI
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "com.google.android.material:material:1.6.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"

    // Architecture Components
    def life_cycle_version = "2.5.0-rc01"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$life_cycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // Navigation
    def navigation_version = "2.4.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$navigation_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$navigation_version"

    // Glide
    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"

    // Coroutine
    def coroutines_version = "1.6.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // KTX
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.fragment:fragment-ktx:1.4.1"

    // Utils
    api "com.jakewharton.timber:timber:5.0.1"
}

kapt {
    correctErrorTypes = true
}
